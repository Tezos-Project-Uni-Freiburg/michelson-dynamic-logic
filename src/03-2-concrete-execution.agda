
module 03-2-concrete-execution where

import 00-All-Utilities as A
open import 01-Types
open import 02-Functions-Interpretations

open import Relation.Binary.PropositionalEquality hiding ([_])
open import Relation.Nullary

open import Data.Nat renaming (_â‰Ÿ_ to _â‰Ÿâ‚™_)
open import Data.List.Base hiding ([_]; head; tail)
open import Data.List.Relation.Unary.All using (All; _âˆ·_; []; head; tail)
open import Data.Maybe.Base hiding (map)
open import Data.Product hiding (map)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Data.Unit using (âŠ¤;tt)

--! Concrete >

variable
  p s : Type

--! MODE
record MODE : Setâ‚ where
  field
    ğ“œ  : Type â†’ Set
    ğ“•  : Set

open MODE

--! CMode
CMode : MODE
CMode = record { ğ“œ = âŸ¦_âŸ§ ; ğ“• = âŠ¤ }

Concrete : âˆ€ {a}{A : Set a} â†’ (MODE â†’ A) â†’ A
Concrete F = F CMode

------------------------- Execution states and program execution ------------------------

-- contracts are parameterized by their parameter and storage types,
-- they provide evidence that these types are appropriate,
-- as well as their BALANCE and STORAGE (values) and a well typed program (NO shadow prog)
--! Contract
record Contract (Mode : MODE) (p s : Type) : Set where
  constructor ctr
  field
    Param    : Passable p
    Store    : Storable s
    balance  : ğ“œ Mode mutez
    storage  : ğ“œ Mode s
    program  : Program [ pair p s ] [ pair (list operation) s ]

variable Mode : MODE

CContract : Type â†’ Type â†’ Set
CContract = Concrete Contract

-- for updating contracts when their execution terminated successfully
update : Contract Mode p s â†’ ğ“œ Mode mutez â†’ ğ“œ Mode s â†’ Contract Mode p s
update c blc srg = record c{ balance = blc ; storage = srg }
updsrg : Contract Mode p s â†’ ğ“œ Mode s â†’ Contract Mode p s
updsrg c     srg = record c{ storage = srg }
updblc : Contract Mode p s â†’ ğ“œ Mode mutez â†’ Contract Mode p s
updblc c blc     = record c{ balance = blc }
subamn : CContract p s â†’ âŸ¦ mutez âŸ§ â†’ CContract p s
subamn c amn     = record c{ balance = Contract.balance c âˆ¸ amn }

-- the blockchain maps any address to a contract if it stores one at that address
--! Blockchain
Blockchain : (Mode : MODE) â†’ Set
Blockchain Mode = âŸ¦ addr âŸ§ â†’ Maybe (âˆƒ[ p ] âˆƒ[ s ] Contract Mode p s)

CBlockchain : Set
CBlockchain = Concrete Blockchain

-- to set an address to a contract on a Blockchain
set : âŸ¦ addr âŸ§ â†’ Contract Mode p s â†’ Blockchain Mode â†’ Blockchain Mode
set adr c bl a
  with a â‰Ÿâ‚™ adr
... | yes refl = just (_ , _ , c)
... | no  Â¬adr = bl a

-- empty Blockchain
âˆ… : Blockchain Mode
âˆ… adr = nothing

-- this is the environment record that holds the informations necessary to execute
-- env-func instructions
-- it can easily be extended to enable more such instructions
-- the fields current and sender are currently not used for instructions but for
-- handling multi-contract executions where execution results are written back to
-- the blockchain and emitted operations can be executed in the same run
record Environment (Mode : MODE) : Set where
  constructor env
  field
    accounts : Blockchain Mode
    current  : âŸ¦ addr âŸ§
    sender   : âŸ¦ addr âŸ§
    balance  : (ğ“œ Mode) mutez
    amount   : (ğ“œ Mode) mutez

CEnvironment : Set
CEnvironment = Concrete Environment

-- PJT: why are balance and amount needed if we can take them from current?

-- this is the program state used to execute any possible Michelson Program on
-- well typed stacks
-- parameterization with the expected output Stacks ensures that the execution
-- model and also the results from contract executions are well typed
-- (meaning they preserve the expected output Stacks)
-- the current stacks are Int's, i.e. typed stacks of values
-- some instructions are expanded to programs that include shadow instructions
record Prog-state (Mode : MODE) (ro so : Stack) : Set where
  constructor state
  field
    {ri si} : Stack
    en  : Environment Mode
    prg : ShadowProg ri si  ro so
    rSI : All (ğ“œ Mode) ri
    sSI : All (ğ“œ Mode) si
    Î¦   : ğ“• Mode
open Prog-state

-- abstract: Î¦ âˆ§ < en | rsi , ssi | prg >
pattern _âˆ§<_âˆ£_,_âˆ£_> Î¦ en rsi ssi prg = state en prg rsi ssi Î¦
-- concrete:
pattern <_âˆ£_,_âˆ£_> en rsi ssi prg = state en prg rsi ssi tt

CProg-state : Stack â†’ Stack â†’ Set
CProg-state = Concrete Prog-state

variable
  ro so : Stack

-- when not executing a single program but entire contracts and blockchain operations
-- this record encapsulates a Prog-state that is parameterized with the typing
-- restrictions for the 'current' contract that is executed
-- the sender is the account that triggered the current contract execution
-- it may be the same as current, and their addersses are saved in the Environment
-- of the Prog-state
-- they are saved in Prg-running because it was easier to implement
-- the update of a successfully terminated contract execution
-- by updating these contracts and saving them back to the blockchain (i.e. setting
-- their addresses on the blockchain to the updated contracts)
-- it could be possible that instead of saving the contract one could save something
-- like Exec-state.accounts current-address â‰¡ just (p , s , Contract p s)
-- but it is probably a lot harder and i couldn't see any benefit in doing so
record Prg-running (Mode : MODE) : Set where
  constructor pr
  field
    {pp ss x y} : Type
    current : Contract Mode pp ss
    sender  : Contract Mode x y
    Ï       : Prog-state Mode [ pair (list ops) ss ] []

CPrg-running : Set
CPrg-running = Concrete Prg-running

-- this is the execution state used to execute entire contracts and blockchain operations
-- when it's in a state where a contract is under execution, MPstate will have a value
-- just prg-running containing an approrpiate and well typed Prog-state
-- otherwise MPstate having the value nothing signals the execution model to handle
-- the next pending operation
-- those are saved as lists of pairs of lists since every contract emits a
-- (possibly empty) list of operations, the address of the emitter will be saved with it.
record Transaction (Mode : MODE) : Set where
  constructor _,_
  field
    pops : (ğ“œ Mode) (list operation)
    psender : âŸ¦ addr âŸ§

CTransaction : Set
CTransaction = Concrete Transaction

record Exec-state (Mode : MODE) : Set where
  constructor exc
  field
    accounts : Blockchain Mode
    MPstate  : Prg-running Mode âŠ ğ“• Mode
    pending  : List (Transaction Mode)

CExec-state : Set
CExec-state = Concrete Exec-state

-- these are all the preliminary constructs necessary to implement
-- the Michelson execution model

-- helper function to easily execute the CONTRACT instruction
appcontract : (P : Passable t) â†’ CEnvironment â†’ âŸ¦ addr âŸ§
         â†’ âŸ¦ option (contract P) âŸ§
appcontract {t} P en adr
  with Environment.accounts en adr
... | nothing = nothing
... | just (p , s , c)
  with p â‰Ÿ t
... | no  _ = nothing
... | yes _ = just adr

-- like appft for Environment Functions, so we also need the environment
-- to implement these
appEF : env-func args result â†’ CEnvironment â†’ Int args â†’ âŸ¦ result âŸ§
appEF AMOUNT  en Iargs = Environment.amount  en
appEF BALANCE en Iargs = Environment.balance en
appEF (CONTRACT P) en (adr âˆ· []) = appcontract P en adr

-- execution model for Program states
-- output stacks are arbitrary but fixed during execution
-- i don't want to mess up the code by including an explanation of every possible
-- execution step and it should'n be necessary. I explain some of them in the thesis
-- (sections 3.1 and 3.2) and the rest should be self explanatory with sufficient
-- knowledge of Michelson (see https://tezos.gitlab.io/michelson-reference)
prog-step : CProg-state ro so â†’ CProg-state ro so
prog-step Ï
  with prg Ï
... | end = Ï
... | fct ft Í¾ p
  = record Ï {  prg = p  ;
                rSI = (appft ft    (A.top (rSI Ï)) A.++ A.bot (rSI Ï)) }
... | enf ef Í¾ p
  = record Ï {  prg = p  ;
                rSI = (appEF ef (en Ï) (A.top (rSI Ï))   âˆ· A.bot (rSI Ï)) }
... | DROP Í¾ p
  = record Ï {  prg = p  ;
                rSI = A.bot (rSI Ï) }
... | DIP n dp Í¾ p
  = record Ï {  prg =   dp Í¾âˆ™ DIP' (take n (ri Ï)) âˆ™ p  ;
                rSI = A.drop n (rSI Ï) ;
                sSI = A.take n (rSI Ï) A.++ (sSI Ï) }
... | ITER ip Í¾ p
  = record Ï {  prg = ITER'    ip âˆ™ p  ;
                rSI = A.drop 1 (rSI Ï) ;
                sSI = head (rSI Ï) âˆ· sSI Ï }
... | IF-NONE thn els Í¾ p
  with rSI Ï
... | just x âˆ· rsi
  = record Ï {  prg = els Í¾âˆ™ p  ;
                rSI =  x âˆ· rsi }
... | nothing âˆ· rsi
  = record Ï {  prg = thn Í¾âˆ™ p  ;
                rSI =      rsi }
prog-step Ï | ITER' ip âˆ™ p
  with sSI Ï
... | [] âˆ· ssi
  = record Ï {  prg = p ;
                sSI = ssi }
... | (x âˆ· xs) âˆ· ssi
  = record Ï {  prg =   ip Í¾âˆ™ ITER'    ip âˆ™ p  ;
                rSI =  x âˆ· rSI Ï ;
                sSI = xs   âˆ· ssi }
prog-step Ï | DIP' top âˆ™ p
  = record Ï {  prg = p  ;
                rSI = A.top (sSI Ï) A.++ rSI Ï ;
                sSI = A.bot (sSI Ï) }


-- execution model of execution states, that is of executions of pending blockchain
-- operations or contract executions
-- when MPstate is just prg-running and the shadow program in its Prog-state matches
-- end, the current contract execution has terminated.
-- because of the typing parameterization the shadow stack must be empty and the
-- real stack must contain the expected single pair of emitted operations new-ops
-- and updated storage value new-storage of the current contract
-- so new-ops can be appended to pending and the relevant contracts can be updated
-- we only allow a transfer-tokens operation to start an actual execution
-- (see below for an explanation on how these are handled)
-- if it comes with enough tokens to support that operations.
-- so at this stage we don't need to check if there were enough.
-- it must be NOTICED!!!! howevere that this will only be enforced automatically
-- when the user initializes an Exec-state with MPstate = nothing and puts the
-- transfer operation to be executed in the pending list. BUT a CARELESS USER
-- could easily program a nonsensical Exec-state where these constraints fail.
-- when the current contract execution hasn't terminated yet, the next Exec-state
-- will be simply that where the Prog-state is executed with prog-step
-- the remaining cases check pending operations, which currently can only contain
-- transfer-tokens operations to start a new contract execution
-- here a lot of parameters have to be checked (described in the thesis but also a bit
-- self explanatory; sadr is the account address that sent the transfer, cadr is the
-- target address)
-- with Contract.balance sender <? tok ensures that only transferes are executed
-- who's amount the sender can actually support, as mentioned above
exec-step : CExec-state â†’ CExec-state
exec-step Ïƒ@(exc
  accounts
  (injâ‚ (pr current sender (state
    (env _ cadr sadr balance amount) end ((new-ops , new-storage) âˆ· []) [] _)))
  pending)
  with cadr â‰Ÿâ‚™ sadr
... | yes _ = record Ïƒ{ accounts = set cadr (updsrg current new-storage) accounts
                      ; MPstate  = injâ‚‚ tt
                      ; pending  = pending ++ [ new-ops , cadr ] }
... | no  _ = record Ïƒ{ accounts = set cadr (update current balance new-storage)
                                 ( set sadr (subamn sender  amount) accounts)
                      ; MPstate  = injâ‚‚ tt
                      ; pending  = pending ++ [ new-ops , cadr ] }
exec-step Ïƒ@(exc _ (injâ‚ Ïr@(pr _ _ Ï)) _)
  = record Ïƒ{ MPstate = injâ‚ (record Ïr{ Ï = prog-step Ï }) }
exec-step Ïƒ@(exc accounts (injâ‚‚ tt) []) = Ïƒ
exec-step Ïƒ@(exc accounts (injâ‚‚ tt) [ [] , sadr // pending ])
  = record Ïƒ{ pending = pending }
exec-step Ïƒ@(exc accounts (injâ‚‚ tt) [ [ transfer-tokens {ty} x tok cadr // more-ops ]
                                  , sadr // pending ])
  with accounts sadr
... | nothing = record Ïƒ{ pending = pending }
... | just (p , _ , sender)
  with sadr â‰Ÿâ‚™ cadr
... | yes _
  with ty â‰Ÿ p
... | no  _    = record Ïƒ{ pending = [ more-ops , sadr // pending ] }
... | yes refl
  = exc accounts
        (injâ‚ (pr sender sender (state
          (env accounts cadr sadr (Contract.balance sender) zero)
          (Contract.program sender Í¾âˆ™ end)
          ((x , Contract.storage sender) âˆ· []) [] _)))
        [ more-ops , sadr // pending ]
exec-step Ïƒ@(exc accounts (injâ‚‚ tt) [ [ transfer-tokens {ty} x tok cadr // more-ops ]
                                  , sadr // pending ])
    | just (_ , _ , sender) | no _
  with Contract.balance sender <? tok | accounts cadr
... | yes _ | _       = record Ïƒ{ pending = [ more-ops , sadr // pending ] }
... | no  _ | nothing = record Ïƒ{ pending = [ more-ops , sadr // pending ] }
... | no  _ | just (p , _ , current)
  with ty â‰Ÿ p
... | no  _    = record Ïƒ{ pending = [ more-ops , sadr // pending ] }
... | yes refl
  = exc accounts
        (injâ‚ (pr current sender (state
          (env accounts cadr sadr (tok + Contract.balance current) tok)
          (Contract.program current Í¾âˆ™ end)
          ((x , Contract.storage current) âˆ· []) [] _)))
        [ more-ops , sadr // pending ]

-- this is just a convenience function to execute several steps at once,
-- it does not faithfully reflect the gas consumption model of Michelson!!!
exec-exec : â„• â†’ CExec-state â†’ â„• Ã— CExec-state
exec-exec zero starved = zero , starved
exec-exec (suc gas) Ïƒ@(exc _ (injâ‚ _) _) = exec-exec gas (exec-step Ïƒ)
exec-exec (suc gas) Ïƒ@(exc _ (injâ‚‚ _) (_ âˆ· _)) = exec-exec gas (exec-step Ïƒ)
exec-exec (suc gas) Ïƒ@(exc _ (injâ‚‚ _) []) = suc gas , Ïƒ

